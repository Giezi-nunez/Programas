CREATE OR REPLACE TRIGGER NEW_EMP_DEPT
INSTEAD OF INSERT OR UPDATE OR DELETE ON EMP_DETAILS
FOR EACH ROW
BEGIN
 IF INSERTING THEN
   INSERT INTO NEW_EMPS
   VALUES (:NEW.EMPLOYEE_ID, :NEW.LAST_NAME, :NEW.SALARY,
           :NEW.DEPARTMENT_ID, :NEW.EMAIL, :NEW.JOB_ID, SYSDATE);
   UPDATE NEW_DEPTS
   SET TOT_DEPT_SAL = TOT_DEPT_SAL + :NEW.SALARY
   WHERE DEPARTMENT_ID = :NEW.DEPARTMENT_ID;
 ELSIF DELETING THEN
   DELETE FROM NEW_EMPS
   WHERE EMPLOYEE_ID = :OLD.EMPLOYEE_ID;
   UPDATE NEW_DEPTS
   SET TOT_DEPT_SAL = TOT_DEPT_SAL - :OLD.SALARY
   WHERE DEPARTMENT_ID = :OLD.DEPARTMENT_ID;
 ELSIF UPDATING ('salary')
 THEN
   UPDATE NEW_EMPS
   SET SALARY = :NEW.SALARY
   WHERE EMPLOYEE_ID = :OLD.EMPLOYEE_ID;
   UPDATE NEW_DEPTS
   SET TOT_DEPT_SAL = TOT_DEPT_SAL + (:NEW.SALARY - :OLD.SALARY)
   WHERE DEPARTMENT_ID = :OLD.DEPARTMENT_ID;
 ELSIF UPDATING ('department_id')
 THEN
   UPDATE NEW_EMPS
   SET DEPARTMENT_ID = :NEW.DEPARTMENT_ID
   WHERE EMPLOYEE_ID = :OLD.EMPLOYEE_ID;
   UPDATE NEW_DEPTS
   SET TOT_DEPT_SAL = TOT_DEPT_SAL - :OLD.SALARY
   WHERE DEPARTMENT_ID = :OLD.DEPARTMENT_ID;
   UPDATE NEW_DEPTS
   SET TOT_DEPT_SAL = TOT_DEPT_SAL + :NEW.SALARY
   WHERE DEPARTMENT_ID = :NEW.DEPARTMENT_ID;
 END IF;
END;
/